## 概述
自定义网格的用途还是很多的，因为有些情况下并不会用到TileMap或不值得用TileMap。
## 定义和绘制网格
```swift
extends Node2D

var grid_size = Vector2i(32,32) # 尺寸 - 有多少行、多少列
var cell_size = Vector2i(32,32) # 单元格大小


func _draw():
	# 绘制网格
	for x in grid_size.x:
		for y in grid_size.y:
			draw_rect(get_cell_rect(Vector2(x,y)),Color.YELLOW,false,1)

# 返回对应单元格的矩形
func get_cell_rect(cell_pos:Vector2i):
	return Rect2(cell_pos * cell_size,cell_size)

```
通过挨个`draw_rect`的方式方式绘制网格，比起`draw_line`方式，代码简单且易用。
![image.png](https://cdn.nlark.com/yuque/0/2023/png/8438332/1686513695149-b081d1db-1c5e-4e7e-9b64-fd1c01558b7d.png#averageHue=%234d4d4d&clientId=u3624db77-b80b-4&from=paste&height=400&id=u3c332b37&originHeight=1570&originWidth=1552&originalType=binary&ratio=3&rotation=0&showTitle=false&size=14429&status=done&style=none&taskId=u63028880-982b-4f1d-9900-8cea1c633db&title=&width=395.3333740234375)
## 获取单元格中心点
```swift
extends Node2D

var grid_size = Vector2i(32,32) # 尺寸 - 有多少行、多少列
var cell_size = Vector2i(32,32) # 单元格大小


func _draw():
	# 绘制网格
	for x in grid_size.x:
		for y in grid_size.y:
			var pos = Vector2(x,y)
			draw_rect(get_cell_rect(pos),Color.YELLOW,false,1)
			draw_circle(get_cell_center(pos),5,Color.YELLOW)

# 返回对应单元格的中心点
func get_cell_center(cell_pos:Vector2):
	return cell_pos * Vector2(cell_size) + Vector2(cell_size/2)

# 返回对应单元格的矩形
func get_cell_rect(cell_pos:Vector2i):
	return Rect2(cell_pos * cell_size,cell_size)

```
`cell_pos * Vector2(cell_size)`返回的单元格左上角点的位置，加上单元格尺寸的一半，就是单元格的中心位置。
![image.png](https://cdn.nlark.com/yuque/0/2023/png/8438332/1686514318467-b3e76a38-1813-4f61-aaf7-1c2a048a6e39.png#averageHue=%234d4d4d&clientId=u3624db77-b80b-4&from=paste&height=400&id=u63d7c56f&originHeight=1571&originWidth=1553&originalType=binary&ratio=3&rotation=0&showTitle=false&size=26084&status=done&style=none&taskId=uac9340cb-6ee1-4462-a5ed-cdcd28aeff8&title=&width=395.66668701171875)
## 将屏幕坐标转化为网格坐标
```swift
extends Node2D

var grid_size = Vector2i(32,32) # 尺寸 - 有多少行、多少列
var cell_size = Vector2i(32,32) # 单元格大小
var target_pos

func _input(event):
	if event is InputEventMouseButton:
		if event.button_index == MOUSE_BUTTON_LEFT and event.is_pressed():
			target_pos = get_grid_pos(get_global_mouse_position())
			queue_redraw()


func _draw():
	# 绘制网格
	for x in grid_size.x:
		for y in grid_size.y:
			var pos = Vector2(x,y)
			draw_rect(get_cell_rect(pos),Color.YELLOW,false,1)
			draw_circle(get_cell_center(pos),5,Color.YELLOW if pos != target_pos else Color.RED)
				

# 返回屏幕中的点或路径中的点对应在网格中的坐标
func get_grid_pos(screen_pos:Vector2):
	return floor(screen_pos / Vector2(cell_size))

# 返回对应单元格的中心点
func get_cell_center(cell_pos:Vector2):
	return cell_pos * Vector2(cell_size) + Vector2(cell_size/2)

# 返回对应单元格的矩形
func get_cell_rect(cell_pos:Vector2i):
	return Rect2(cell_pos * cell_size,cell_size)

```
`floor(screen_pos / Vector2(cell_size))`获得的是屏幕坐标点击的单元格所在的坐标。
![自定义绘制网格.gif](https://cdn.nlark.com/yuque/0/2023/gif/8438332/1686514625190-d8c67244-3eb3-49a6-89a3-480005cc0df5.gif#averageHue=%234c4c4c&clientId=u3624db77-b80b-4&from=drop&height=398&id=u8ab65d75&originHeight=1550&originWidth=1545&originalType=binary&ratio=3&rotation=0&showTitle=false&size=159832&status=done&style=none&taskId=u5ddd4629-e341-4a72-b152-8d79d0c588b&title=&width=397)
## 获取行走相应步数的有效矩形
以下代码暂时存在Bug。
```swift
extends Node2D

var grid_size = Vector2i(32,32) # 尺寸 - 有多少行、多少列
var cell_size = Vector2i(32,32) # 单元格大小
var target_pos
var can_walk_rect:Rect2i

func _input(event):
	if event is InputEventMouseButton:
		if event.button_index == MOUSE_BUTTON_LEFT and event.is_pressed():
			target_pos = get_grid_pos(get_global_mouse_position())
			can_walk_rect = get_can_walk_rect(target_pos,5)
			print(can_walk_rect)
			queue_redraw()


func _draw():
	# 绘制网格
	for x in grid_size.x:
		for y in grid_size.y:
			var pos = Vector2(x,y)
			draw_rect(get_cell_rect(pos),Color.YELLOW,false,1)
			draw_circle(get_cell_center(pos),5,Color.YELLOW if pos != target_pos else Color.RED)
			
	# 绘制可行走范围
	for x in range(can_walk_rect.position.x,can_walk_rect.end.x):
		for y in range(can_walk_rect.position.y,can_walk_rect.end.y):
			var pos = Vector2(x,y)
			draw_rect(get_cell_rect(pos),Color(Color.GREEN_YELLOW,0.5),true,1)
	

# 返回屏幕中的点或路径中的点对应在网格中的坐标
func get_grid_pos(screen_pos:Vector2):
	return floor(screen_pos / Vector2(cell_size))

# 返回对应单元格的中心点
func get_cell_center(cell_pos:Vector2):
	return cell_pos * Vector2(cell_size) + Vector2(cell_size/2)

# 返回对应单元格的矩形
func get_cell_rect(cell_pos:Vector2i):
	return Rect2(cell_pos * cell_size,cell_size)

# 获取对应单元格在整个网格中可以行走相应步数的有效矩形
# cell_pos：单元格的位置
# step：行走的步数
func get_can_walk_rect(cell_pos:Vector2,step:int):
	var top_left = clamp(cell_pos - Vector2.ONE * step,Vector2.ZERO,Vector2(grid_size))
	var end =  clamp(cell_pos + Vector2.ONE * step,Vector2.ZERO,Vector2(grid_size))
	return Rect2(top_left,clamp(end - top_left,Vector2.ZERO,Vector2.ONE * 10))

```
